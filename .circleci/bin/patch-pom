#!/bin/sh
#_(
   #_DEPS is same format as deps.edn. Multiline is okay.
   DEPS='
   {:deps {org.clojure/data.xml {:mvn/version "0.2.0-alpha6"}
           org.clojure/data.zip {:mvn/version "0.1.3"}}}
   '

   #_You can put other options here
   OPTS='
     -J-Xms256m -J-Xmx256m -J-client
   '

exec clojure $OPTS -Sdeps "$DEPS" "$0" "$@"
)

(ns fc4.ci.patch-pom
  "Patches a Maven POM file generated by tools.deps. Thereâ€™s a bug in tools.deps wherein the
  generated POM has an empty `repositories` element. This script replaces the empty repositories
  element with a non-empty element that has the repositories we need for the POM to be useful via
  Maven and therefore enable us to scan our project for security vulnerabilities using Snyk.

  This bug is present in Clojure CLI tools 1.10.1.502 but should be fixed in the next version."
  (:require [clojure.data.xml :as xml :refer [element]]
            [clojure.data.zip.xml :as zx]
            [clojure.zip :as zip]))

(xml/alias-uri 'pom "http://maven.apache.org/POM/4.0.0")

(def better-repositories-elem
  (element ::pom/repositories {}
    (element ::pom/repository {}
      (element ::pom/id {} "central")
      (element ::pom/url {} "https://repo1.maven.org/maven2/"))
    (element ::pom/repository {}
      (element ::pom/id {} "clojars")
      (element ::pom/url {} "https://repo.clojars.org/"))))

(defn- patch
  [root-elem-zip]
  (-> (zx/xml1-> root-elem-zip ::pom/repositories)
      (zip/edit (constantly better-repositories-elem))
      (zip/root)))

(defn- boom
  []
  (->> (slurp "pom.xml")
       (xml/parse-str)
       (zip/xml-zip)
       (patch)
       (xml/indent-str)
       (spit "pom.xml")))

(boom)
